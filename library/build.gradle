apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

gradle.ext.isCiServer = System.getenv().containsKey("CI")
logger.warn("Running on CI: ${gradle.ext.isCiServer}")

if (gradle.ext.isCiServer) {
    apply plugin: "com.vanniktech.maven.publish"
    mavenPublish {
        releaseRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
        snapshotRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

test {
    useJUnitPlatform {
        includeEngines "spek2"
    }
}

dependencies {
    api libraries.kotlinStdlib
    api libraries.coroutinesCore

    testImplementation testLibraries.spek2Dsl
    testRuntimeOnly testLibraries.spek2Junit5Runner
    testRuntimeOnly testLibraries.junit5Engine

    testImplementation libraries.rxJava
    testImplementation libraries.coroutinesRx2
    testImplementation testLibraries.junit
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile.class).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
    }
}
