apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'shot'

kapt {
    arguments {
        arg("moshi.generated", "javax.annotation.Generated")
    }
}

shot {
    appId = 'com.freeletics.coredux'
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.freeletics.coredux"
        targetSdkVersion versions.targetSdk
        minSdkVersion versions.minSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.freeletics.coredux.SampleAppRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        test {
            java.srcDirs += ['src/testSpec/java']
            resources.srcDirs += ['src/testSpec/resources']
        }

        androidTest {
            java.srcDirs += ['src/testSpec/java']
            resources.srcDirs += ['src/testSpec/resources']
        }
    }

    compileOptions {
        targetCompatibility = JavaVersion.VERSION_1_8
        sourceCompatibility = JavaVersion.VERSION_1_8
    }

    testOptions {
        animationsDisabled = true
    }

    lintOptions {
        disable 'GoogleAppIndexingWarning','InvalidPackage'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile.class).all {
    kotlinOptions {
        freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
    }
}

dependencies {
    implementation libraries.kotlinStdlib
    implementation supportLibraries.appCompat
    implementation supportLibraries.recyclerView
    implementation supportLibraries.constraintLayout
    implementation supportLibraries.design
    implementation supportLibraries.viewModel
    implementation libraries.retrofit
    implementation libraries.retrofitMoshi
    implementation libraries.rxJava
    implementation libraries.timber
    implementation libraries.rxBinding
    implementation libraries.okhttp
    implementation libraries.moshiKotlin
    implementation libraries.moshi
    implementation libraries.retrofitCoroutines
    implementation libraries.coroutinesAndroid

    kapt libraries.moshiCodeGen
    implementation libraries.moshi
    implementation libraries.dagger
    kapt libraries.daggerCompiler
    implementation project(':library:core')
    implementation project(':library:log:android')
    implementation testLibraries.okhttpTls

    testImplementation testLibraries.junit
    testImplementation testLibraries.archCoreTest
    testImplementation testLibraries.mockWebServer

    kaptTest libraries.daggerCompiler
    androidTestImplementation testLibraries.junit
    androidTestImplementation testLibraries.testRunner
    androidTestImplementation testLibraries.espressoCore
    androidTestImplementation testLibraries.espressoContrib
    androidTestImplementation testLibraries.testRules
    androidTestImplementation testLibraries.screengrab
    androidTestImplementation testLibraries.deviceAnimationRule
    androidTestImplementation testLibraries.mockWebServer
    androidTestImplementation libraries.moshiKotlin
    androidTestImplementation libraries.moshi
}
